/*
 * Copyright 2016 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

plugins {
    id 'java'
    id 'io.github.humblerookie.gradle' version '0.5.0'
}

apply from: 'plugin-helpers.gradle'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

project.ext.pluginVersion = '2.1.1'
project.ext.fullVersion = project.git.distVersion() ? "${project.pluginVersion}-${project.git.distVersion()}" : project.pluginVersion

group = 'com.thoughtworks.go'
version = project.fullVersion

project.ext.pluginDesc = [
        id         : 'nuget',
        repo       : 'go-nuget-poller-plugin-2.0',
        version    : project.version,
        goCdVersion: '20.9.0',
        name       : 'Nuget plugin',
        description: 'NuGet Poller Plugin for GoCD',
        vendorName : 'GoCD Contributors',
        vendorUrl  : 'https://github.com/gocd-contrib/go-nuget-poller-plugin-2.0'
]

repositories {
    mavenCentral()
}

ext {
    deps = [
        gocdPluginApi: 'cd.go.plugin:go-plugin-api:24.4.0',
    ]

    versions = project.ext.deps.collectEntries { lib, libGav -> [lib, libGav.split(':').last()] }
}

dependencies {
    compileOnly project.deps.gocdPluginApi
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'commons-io:commons-io:2.18.0'
    implementation group: 'com.squareup.okhttp', name: 'okhttp', version: '2.7.5'
    implementation files('libs/go-plugin-util.jar')
    implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'

    testImplementation project.deps.gocdPluginApi
    testImplementation 'org.mockito:mockito-core:5.14.2'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.27.0'
    testImplementation platform('org.junit:junit-bom:5.11.3')
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
    testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

jar {
    from(configurations.runtimeClasspath) {
        into "lib/"
    }
}

apply from: 'plugin-tasks.gradle'
